{-# STDLIB_VERSION 3 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}

let startNewRoundHeight = 5
let idGameToken =  base58'6abp1c1jcDVnTvYhiuCReCpnW4njtT8wSFtN5K47n79D'
let minBet = 100000
let maxNumberRound = 8

# create random hash for last round
func randomizer(inv: Invocation) = {
    let lastPlay = match getBinary(this, "lastPlay") {
        case s:ByteVector => s
        case a:Unit => base64'V2F2ZXNMb3R0b1Yy'
    }

    let rand = lastPlay + inv.transactionId + inv.callerPublicKey + lastBlock.generationSignature + toBytes(lastBlock.timestamp) + toBytes(lastBlock.height)
    sha256(rand)
}

func getCurrentNumberRound() = {
  match getInteger(this, "numberRound") {
    case a:Int => a
    case _ => 1
  }
}

func getCurrentNumberGame() = {
  match getInteger(this, "numberGame") {
    case a:Int => a
    case _ => 1
  }
}
func getPrevNumberGame() = {
  getCurrentNumberGame() - 1
}

func getCurrentGameAndRound() = {
  getCurrentNumberGame().toString() + "_" + getCurrentNumberRound().toString()
}

func getCurrentGameAndPrevRound() = {
  let prevRound = getCurrentNumberRound() - 1
  getCurrentNumberGame().toString() + "_" + prevRound.toString() + "_"
}
func saveWinnerInfoPrevRound(currRound: Int, winnerFlag: String, randhash: Int) = {
   WriteSet([
      DataEntry(getCurrentGameAndPrevRound() + "lastRoundWinner", getCurrentGameAndPrevRound() + winnerFlag ),
      DataEntry("numberRound", currRound) ,
      DataEntry("startGame", true),
      DataEntry("randhash", randhash)
      ]
    )
}
func setWinnersPrevRound(currRound: Int, randhash: Int) = {
  if (randhash < 500) then
    saveWinnerInfoPrevRound(currRound,"less_", randhash)
  else
    saveWinnerInfoPrevRound(currRound, "more_", randhash)
}

func getGamerBet(addressGamer: String, direction: String) = {
  match getInteger(this, getCurrentGameAndRound() + "_" + direction + "_" + addressGamer) {
        case g:Int => g
        case _ => 0
  }
}
func defineWinnerSign(hash: Int) = {
  if(hash <= 500) then
    "tails"
  else
    "eagle"
}

func defineCoinPart(part: String) = {
  if(part == "_less_") then
    "tails"
  else
    "eagle"
}

func setGamerBet(addressGamer: String, directionRate: String, directionGamer: String, currentBet: Int) = {
    let newNumberOfRound = getCurrentNumberRound()
    let numOfGamersCurrDirection = match getInteger(this, getCurrentGameAndRound() + directionGamer) {
      case g:Int => g
      case _ => 0
    }
    let sumOfBank = match getInteger(this, getCurrentNumberGame().toString() + "_BankOfGame") {
      case g:Int => g
      case _ => 0
    }
    let sumGamerBets = match getInteger(this, getCurrentNumberGame().toString() + "_" + addressGamer) {
      case s:Int => s
      case _ => 0
    }
    if(maxNumberRound == newNumberOfRound + 1) then
      WriteSet([
        DataEntry(getCurrentGameAndRound() + directionRate + addressGamer, currentBet),
        DataEntry(getCurrentGameAndRound() + directionGamer, numOfGamersCurrDirection + 1),
        DataEntry(getCurrentNumberGame().toString() + "_BankOfGame", sumOfBank + currentBet),
        DataEntry(getCurrentNumberGame().toString() + "_" + addressGamer, sumGamerBets + currentBet),
        DataEntry(getCurrentNumberGame().toString() + "_" + addressGamer + "_" + defineCoinPart(directionRate) + "_GetPrize", false)
      ])
    else
    WriteSet([
      DataEntry(getCurrentGameAndRound() + directionRate + addressGamer, currentBet),
      DataEntry(getCurrentGameAndRound() + directionGamer, numOfGamersCurrDirection + 1),
      DataEntry(getCurrentNumberGame().toString() + "_BankOfGame", sumOfBank + currentBet),
      DataEntry(getCurrentNumberGame().toString() + "_" + addressGamer, sumGamerBets + currentBet)
      ])
}

func getBet(round: Int, addressGamer: String) =  {

  if(round > 1 ) then {
    let winnerOfPrevRound = getCurrentGameAndPrevRound()
    let winnerBetFromPrevRound = match getInteger(this, winnerOfPrevRound + addressGamer) {
      case w: Int => w
      case _ => 0
    }
    let ratesGamerInGame = match getInteger(this, getCurrentNumberGame().toString() + "_" + addressGamer) {
          case r: Int => r
          case _ => 0
        }
    if(winnerBetFromPrevRound != 0) then
       minBet
    else if (winnerBetFromPrevRound == 0 && ratesGamerInGame != 0) then
      minBet # * round * 2  - ratesGamerInGame
    else if (winnerBetFromPrevRound == 0 && ratesGamerInGame == 0) then
      minBet * round * 2
    else
      minBet
  }
  else
    minBet
}

func isOppositeRate(numberOfRound: Int) = {
    let numberOfGamersUp = match getInteger(this, getCurrentNumberGame().toString() + "_" + numberOfRound.toString() + "_countGamersUp") {
      case g:Int => g
      case _ => 0
    }
    let numberOfGamersDown = match getInteger(this, getCurrentNumberGame().toString() + "_" + numberOfRound.toString() + "_countGamersDown") {
      case g:Int => g
      case _ => 0
    }
    numberOfGamersUp > 0 && numberOfGamersDown > 0
}

func getNextNumberGame() = {
  match getInteger(this, "numberGame") {
    case a:Int => a + 1
    case _ => 1
  }
}

func saveDataStartGame(numberGame: Int, isGameStarted: Boolean, numberOfRound: Int ) = {
 WriteSet([
      DataEntry("numberGame", numberGame),
      DataEntry("startGame", isGameStarted),
      DataEntry("numberRound", numberOfRound),
      DataEntry(numberGame.toString() + "_BankOfGame", 0)
      ])
}

@Callable(i)
func startRound() = {
  let numberOfRound = getCurrentNumberRound()
  let prev = numberOfRound - 1
  let randhash = randomizer(i)
  let isGameStarted = match getBoolean(this, "startGame") {
    case g:Boolean => g
    case _ => false
  }
  let currentNumberGame = getCurrentNumberGame()
  if(i.caller != this) then
    throw("Only owner can start the game.")
  else if( numberOfRound == 1 && !isOppositeRate(numberOfRound) && i.caller == this && !isGameStarted ) then

    #invoke script
    # setGameOver(currentNumberGame)  ?
    # let newNumberGame = currentNumberGame
    saveDataStartGame(currentNumberGame, !isGameStarted, numberOfRound)
  else if (i.caller == this && isOppositeRate(prev) && !isGameStarted) then
    setWinnersPrevRound(numberOfRound, randhash.toInt() % 1000)
  else if (numberOfRound == 0 && currentNumberGame > 1) then
    WriteSet([
      DataEntry(getPrevNumberGame().toString() + "_WinnerSign", defineWinnerSign(randhash.toInt() % 1000)),
      DataEntry("numberRound", numberOfRound + 1)
    ])
  else if (numberOfRound == 0 && isGameStarted) then
    WriteSet([
      DataEntry("numberRound", numberOfRound + 1)
    ])
  else if( numberOfRound > 1 && isOppositeRate(prev) && i.caller == this && !isGameStarted ) then
    WriteSet([
      DataEntry("startGame", !isGameStarted)
    ])
  else
    throw("startRound error case." + isGameStarted.toString() + isOppositeRate(prev).toString())
}

@Callable(i)
func stopGame() = {
  let isGameStarted = match getBoolean(this, "startGame") {
    case g:Boolean => g
    case _ => false
  } #isOppositeRate() &&  && !isGameStarted
  let lastRound = getCurrentNumberRound()
  let newNumberOfRound = (lastRound + 1) % maxNumberRound

  if(i.caller == this && newNumberOfRound >= 1 && isOppositeRate(lastRound)) then
    WriteSet([
      DataEntry("numberRound", newNumberOfRound)
    ])
  else if (newNumberOfRound <= 1) then
    if(newNumberOfRound == 1) then
      let newGame = getNextNumberGame()
      saveDataStartGame(newGame, isGameStarted, newNumberOfRound)
    else
      WriteSet([
        DataEntry(getCurrentNumberGame().toString() + "_GameOver", true),
        DataEntry("numberGame", getCurrentNumberGame() + 1),
        DataEntry("numberRound", newNumberOfRound)
      ])
  else
    throw("Only owner can stop the game")
}

@Callable(i)
func makeBetLess() = {
  let isGameStarted = match getBoolean(this, "startGame") {
    case g:Boolean => g
    case _ => false
  }
  let pmt = extract(i.payment)
  let addressGamer = toBase58String(i.caller.bytes)
  let currentRound = getCurrentNumberRound()
  let countBet = getBet(currentRound, addressGamer)
  let gamerAlreadyOppositeBet = getGamerBet(addressGamer, "more")
  if (gamerAlreadyOppositeBet != 0 || pmt.assetId != idGameToken) then
    throw("The gamer already made opposite a bet in the round!")
  else if(countBet > pmt.amount) then
    throw("Payment must be more for the round!" + countBet.toString() + pmt.amount.toString() + currentRound.toString() + addressGamer)
  else if (gamerAlreadyOppositeBet == 0 && pmt.assetId == idGameToken && countBet <= pmt.amount && isGameStarted) then
    setGamerBet(addressGamer, "_less_", "_countGamersDown", pmt.amount)
  else
    throw("Less throw"+ gamerAlreadyOppositeBet.toString() + countBet.toString() + pmt.amount.toString() + isGameStarted.toString())
}

@Callable(i)
func makeBetMore() = {
  let isGameStarted = match getBoolean(this, "startGame") {
    case g:Boolean => g
    case _ => false
  }
  let pmt = extract(i.payment)
  let addressGamer = toBase58String(i.caller.bytes)
  let currentRound = getCurrentNumberRound()
  let countBet = getBet(currentRound, addressGamer)
  let gamerAlreadyOppositeBet = getGamerBet(addressGamer, "less")
  if (gamerAlreadyOppositeBet != 0 || pmt.assetId != idGameToken) then
    throw("The gamer already made opposite bet in the round! Or send not true tokens" + gamerAlreadyOppositeBet.toString() + " " +  (pmt.assetId == idGameToken).toString())
  else if(countBet > pmt.amount) then
    throw("Payment must be more for the round!" + countBet.toString() + pmt.amount.toString() + currentRound.toString() + addressGamer)
  else if (gamerAlreadyOppositeBet == 0 && pmt.assetId == idGameToken && countBet <= pmt.amount && isGameStarted) then
    setGamerBet(addressGamer, "_more_", "_countGamersUp", pmt.amount)
  else
    throw("More throw"+ gamerAlreadyOppositeBet.toString() + countBet.toString() + pmt.amount.toString() + isGameStarted.toString())
}

@Callable(i)
func withdraw(numberGame: String) = {
  let addressGamer = toBase58String(i.caller.bytes)
  # проверяем закончилась ли эта игра, есть ли этот адрес в победителях, не забрал ли уже этот
  # адрес приз из этой игры, получаем значение приза проверяем больше ли баланс чем приз, и пересылаем на адрес,
  # отнимаем из общей суммы призов, помечаем что адрес получил приз
  let isGameOver = match getBoolean(this, numberGame + "_GameOver") {
    case a:Boolean => a
    case _ => false
  }
  # получаем орел или решка победили в игре а может число
  let winnerSign = match(this.getString( numberGame + "_WinnerSign")) {
    case a:String => a
    case _ => ""
  }
  # проработать как получить адрес победителя, создавать на последнем раунде запись адресов
  # орел/решка и теперь запрашивать по ключам номера игры и победной решки/орла найти адрес
  # в победителях иначе пустая строка или false
  let isWinner = match getBoolean(this, numberGame + "_" + addressGamer + "_" + winnerSign){
      case x: Boolean => x
      case _ => false
  }
  let isHasPrize = match getBoolean(this, numberGame + addressGamer + "_GetPrize") {
    case a:Boolean => a
    case _ => false
  }
  let valuePrize = match getInteger(this, numberGame + "_ValuePrize") {
    case a:Int => a
    case _ => 0
  }
  let bankOfGame = match getInteger(this, numberGame + "_BankOfGame") {
    case a:Int => a
    case _ => 0
  }

  if(isGameOver && isWinner && isHasPrize && bankOfGame >= valuePrize) then
    ScriptResult(
      WriteSet([
              DataEntry(numberGame + addressGamer + "_GetPrize", true),
              DataEntry(numberGame + "_BankOfGame", bankOfGame - valuePrize)
          ]),
      TransferSet([ScriptTransfer(i.caller, valuePrize, idGameToken)])
    )
  else
    throw("The address is not the winner" + isGameOver.toString() + isWinner.toString() + isHasPrize.toString() +  bankOfGame.toString() + valuePrize.toString() )
}

@Verifier(tx)
func verify() = {
    match(tx) {
        case t:DataTransaction => true # should be false for production
        case t:SetScriptTransaction => true # should be false for production
        case _ => sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
    }
}
